export ZSH=$HOME/.dotfiles

typeset -U config_files
config_files=($ZSH/**/*.zsh)


### ENVIRONMENT

export EDITOR='vim'
export VISUAL='vim'
export BROWSER='google-chrome'
export PAGER='less'

# Colors for less without compiled termcap files (curses feature)
export LESS_TERMCAP_mb=$'\e[01;31m'
export LESS_TERMCAP_md=$'\e[01;38;5;74m'
export LESS_TERMCAP_me=$'\e[0m'
export LESS_TERMCAP_se=$'\e[0m'
export LESS_TERMCAP_so=$'\e[38;5;246m'
export LESS_TERMCAP_ue=$'\e[0m'
export LESS_TERMCAP_us=$'\e[04;38;5;146m'

export LESS=XFRaeiM # the XFR is important here: don't mess with the terminal!

# Load the path files
for file in ${(M)config_files:#*/path.zsh}; do
    source $file
done

# Make a help command available, something like bash :-)
autoload -U run-help
case $(type run-help) in
    *alias*) unalias run-help
esac
alias help=run-help
HELPDIR=/usr/local/share/zsh/help


### ANTIGEN <3

if (( $+commands[brew] )); then
    source `brew --prefix`/share/antigen.zsh
else
    [[ ! -d $ZSH/lib/antigen ]] &&
        mkdir -p $ZSH/lib && git clone https://github.com/zsh-users/antigen.git $ZSH/lib/antigen
    source $ZSH/lib/antigen/antigen.zsh
fi

antigen use oh-my-zsh

if (( $+commands[brew] )); then
    antigen bundle brew
    antigen bundle brew-cask
fi

antigen bundle composer
antigen bundle git
antigen bundle git-extras
antigen bundle grunt
antigen bundle history
antigen bundle npm
antigen bundle pip
antigen bundle sublime
antigen bundle z

antigen apply

# Load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
    source $file
done

# Initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit && compinit

# Load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
    source $file
done

unset config_files
